(* an attempt to capture the Icarus grammar in EBNF notation
   this file also includes a partial 'attribute grammar' which attempts to show
   a mapping from Icarus constructs to the corresponding C *)

program = decl* ;

decl = union-decl
     | type-decl
     | func-decl ;

union-decl = "union", type-decl-name, type-decl-body, "end" ;
type-decl = "type", type-decl-name, type-decl-body, "end" ;
(*[ "typedef struct", type-decl-name, "{", type-decl-body, "} ", type-decl-name, ";" ]*)

type-decl-name = symbol ;
type-decl-body = field* ;
(*[ ForEach(field, [field, ";"]) ]*)

field = symbol, "::", type-ref ;
(*[ type-ref, " ", symbol ]*)

type-ref = symbol ;

func-decl = ("func" | "fn"), func-decl-name, "(", func-decl-arg-list, ")", ("->", type-ref)?, body, "end" ;
func-decl-name = symbol ;
func-decl-arg-list = (field, (",", field)*)? ;
(*[ ForEach(field, [field, ", "]) ]*)

body = stmt* ;

stmt = let-stmt
     | return-stmt
     | assignment-stmt
     | if-stmt
     | for-stmt
     | while-stmt
     | match-stmt
     | function-call ;

let-stmt = "let", symbol, ("::" type-ref)?, "=", expr ;
(*[ type-ref, " ", symbol, " = ", expr ]*)

(* TODO FIXME optional expr here leads to ambiguity *)
return-stmt = "return", expr? ;

if-stmt = "if", expr, body, ("elseif", expr, body)*, ("else", body)?, "end" ;
(*[ "if (", expr, "){", body, }",
    ForEach("elseif", [" else if (", expr, "){", body, "}"]),
    ForEach("else", [" else {", body, "}"]) ]*)

for-stmt = "for", for-lhs, "in", expr, body, "end" ;
for-lhs = symbol ;

while-stmt = "while", expr, body, "end" ;
(*[ "while (", expr, "){", body, "}" ]*)

match-stmt = "match", expr, match-body, "end" ;
(*[ "switch (", expr, "->tag){", match-body, "}" ]*)
match-body = ("case", symbol, "::", type-ref, body)+, ("else", body)?, "end" ;
(*[ ForEach("case", ["case ", type-ref, ": ", body, "break;"]),
    ForEach("else", ["default: ", body, "break;"]) ]*)

(* fcall and assignment are ambiguous, not LL(1) *)
function-call = symbol, "(", function-call-arg-list, ")" ;
function-call-arg-list = (expr, (",", expr)*)? ;

assignment-stmt = assignment-stmt-lhs, "=", expr ;
assignment-stmt-lhs = symbol ;

(* TODO FIXME expr undefined *)
expr = ;

(* once array access and generics are added the following input
    foo[bar,...]...
   will be ambiguous.
   if followed by `=` then this is an assignment into an array
   if followed by `(` then this is a function call
   if followed by `.` then this is a member access on a struct
 *)


letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z"
       | "a" | "b" | "c" | "d" | "e" | "f" | "g"
       | "h" | "i" | "j" | "k" | "l" | "m" | "n"
       | "o" | "p" | "q" | "r" | "s" | "t" | "u"
       | "v" | "w" | "x" | "y" | "z" ;

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

symbol = letter , { letter | digit | "_" } ;


